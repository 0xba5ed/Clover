apply plugin: 'com.android.application'

/**
 * Gets the version name from the latest Git tag
 */
def getCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return "-" + stdout.toString().trim()
}

android {
    compileSdkVersion 26
    // update the travis config when changing this
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25

        versionName "v2.3.1"
        // of the format XXYYZZ, where XX is major, YY is minor, ZZ is patch
        // (watch out for octal notation, never start with a 0)
        versionCode 20301
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }

    lintOptions {
        abortOnError false
    }

    // Needed for volley
    useLibrary 'org.apache.http.legacy'

    /*
    If you want to sign releases, make a file in app/keys.properties with the following content:
    keystoreFile=yourkey.store
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */

    File f = file('keys.properties')
    boolean doSign = f.exists();

    if (doSign) {
        Properties props = new Properties()
        InputStream is = new FileInputStream(f)
        props.load(is)

        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }

        is.close()
    }

    defaultPublishConfig "default"
    flavorDimensions "default"
    productFlavors {
        // The app name refers to the name as displayed on the launcher.
        // the flavor name is appended to the name in the settings.
        "default" {
            dimension "default"
            applicationId "org.floens.chan"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", ""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://floens.github.io/Clover/api/update\""
        }

        dev {
            dimension "default"
            applicationId "org.floens.chan.dev"
            resValue "string", "app_name", "Clover dev"
            resValue "string", "app_flavor_name", ""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"\""
        }

        fdroid {
            dimension "default"
            applicationId "org.floens.chan"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", "F-Droid"
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://floens.github.io/Clover/api/update\""
        }
    }

    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles 'proguard.cfg'
        }

        debug {
            versionNameSuffix getCommitHash()
//            minifyEnabled true
//            proguardFiles 'proguard.cfg'
        }
    }

    sourceSets {
        beta.java.srcDirs = ['src/release/java']
    }
}

dependencies {
    def supportVersion = '26.1.0'

    implementation "com.android.support:support-v13:${supportVersion}"
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation "com.android.support:recyclerview-v7:${supportVersion}"
    implementation "com.android.support:cardview-v7:${supportVersion}"
    implementation "com.android.support:support-annotations:${supportVersion}"
    implementation "com.android.support:design:${supportVersion}"
    implementation "com.android.support:customtabs:${supportVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'org.jsoup:jsoup:1.9.2'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.1.16'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    implementation 'de.greenrobot:eventbus:2.4.0'
    implementation 'org.nibor.autolink:autolink:0.6.0'
    implementation 'com.google.code.gson:gson:2.8.1'

    // Yes that's dagger 1, "deprecated".
    // There's little wrong with it, except that it might be slow at runtime.
    // We don't have a huge graph. The major downside of the newer dagger version
    // is that it uses annotation processing. Annotation processors block incremental
    // compiling, so our builds become way slower, and I rather have faster builds.
    // Move to gradle 2 when incremental compiling supports annotation processors.
    compile 'com.squareup.dagger:dagger:1.2.5'
    provided 'com.squareup.dagger:dagger-compiler:1.2.5'
}
