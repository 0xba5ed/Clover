apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    /* in app/version.properties:
    code=10
    name=v1.0
    */

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    Properties props = new Properties()
    props.load(new FileInputStream(file('keys.properties')))

    /* in app/keys.properties:
    keystoreFile=key.keystore
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */

    signingConfigs {
        release {
            storeFile file(props['keystoreFile'])
            storePassword props['keystorePass']
            keyAlias props['keyAlias']
            keyPassword props['keyPass']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }

        beta.initWith(buildTypes.release)
        beta {
            versionNameSuffix " Beta"
        }

        debug {
            versionNameSuffix " Debug"
        }
    }

    sourceSets {
        beta.java.srcDirs = ['src/release/java']
    }
}

dependencies {
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.android.support:support-v13:18.0.+'

    compile files('libs/httpclientandroidlib-1.1.2.jar')
}
